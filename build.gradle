plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.4.31'
}

group 'ca.jahed.rtpoet'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        mavenLocal()
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/qumase/rtpoet")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GH_USER")
                password = project.findProperty("gpr.key") ?: System.getenv("GH_TOKEN")
            }
        }
    }

    publications {
        mvn(MavenPublication) {
            from components.java
            artifactId = rootProject.name
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'RTPoet'
                description = 'POJO representation for UML-RT models'
                url = 'https://github.com/qumase/rtpoet'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'kjahed'
                        name = 'Karim Jahed'
                        email = 'jahed@cs.queensu.ca'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/qumase/rtpoet.git'
                    developerConnection = 'scm:git:ssh://github.com/qumase/rtpoet.git'
                    url = 'https://github.com/qumase/rtpoet/'
                }
            }
        }
    }
}
